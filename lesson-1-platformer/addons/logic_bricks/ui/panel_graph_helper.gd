extends RefCounted

var panel = null

func setup(target_panel) -> void:
	panel = target_panel

func sync_graph_ui_to_bricks() -> void:
	for graph_node in panel.graph_edit.get_children():
		if not (graph_node is GraphNode):
			continue
		if not graph_node.has_meta("brick_data"):
			continue

		var brick_data = graph_node.get_meta("brick_data")
		var brick_instance = brick_data.get("brick_instance", null)
		if brick_instance == null:
			continue

		for child in graph_node.get_children():
			if not child.has_meta("property_name"):
				continue

			var property_name = str(child.get_meta("property_name"))
			var control = child.get_node_or_null("PropertyControl_" + property_name)

			if control == null:
				if child is CheckBox:
					brick_instance.set_property(property_name, child.button_pressed)
				continue

			if control is SpinBox:
				var spinbox: SpinBox = control
				var current_value = spinbox.value
				var property_type = TYPE_NIL
				for prop_def in brick_instance.get_property_definitions():
					if prop_def.get("name", "") == property_name:
						property_type = int(prop_def.get("type", TYPE_NIL))
						break
				if property_type == TYPE_INT:
					brick_instance.set_property(property_name, int(current_value))
				else:
					brick_instance.set_property(property_name, current_value)
			elif control is CheckBox:
				brick_instance.set_property(property_name, control.button_pressed)
			elif control is OptionButton:
				var option_button: OptionButton = control
				var selected = option_button.selected
				if selected >= 0:
					var value = option_button.get_item_metadata(selected)
					if value == null:
						value = option_button.get_item_text(selected).to_lower().replace(" ", "_")
					brick_instance.set_property(property_name, value)
			elif control is LineEdit:
				var line_edit: LineEdit = control
				if not line_edit.editable and not line_edit.tooltip_text.is_empty():
					brick_instance.set_property(property_name, line_edit.tooltip_text)
				else:
					brick_instance.set_property(property_name, line_edit.text)
			elif control is ColorPickerButton:
				brick_instance.set_property(property_name, control.color)

func is_brick_in_complete_chain(graph_node: GraphNode, connections: Array) -> bool:
	if not graph_node.has_meta("brick_data"):
		return true
	var brick_data: Dictionary = graph_node.get_meta("brick_data")
	var brick_type := str(brick_data.get("brick_type", ""))
	if brick_type == "sensor":
		return _sensor_reaches_terminal(graph_node, connections, {})
	if brick_type == "controller":
		return _controller_has_trigger_upstream(graph_node, connections, {}) and _controller_reaches_terminal(graph_node, connections, {})
	if brick_type == "actuator":
		return _actuator_has_trigger_upstream(graph_node, connections)
	return true


func _sensor_reaches_terminal(sensor_node: GraphNode, connections: Array, visited: Dictionary) -> bool:
	if visited.has(sensor_node.name):
		return false
	var next_visited := visited.duplicate()
	next_visited[sensor_node.name] = true
	for output_node in trace_outputs(sensor_node.name, connections):
		if not output_node.has_meta("brick_data"):
			continue
		var data: Dictionary = output_node.get_meta("brick_data")
		if str(data.get("brick_type", "")) == "controller" and _controller_reaches_terminal(output_node, connections, next_visited):
			return true
	return false


func _controller_has_trigger_upstream(controller_node: GraphNode, connections: Array, visited: Dictionary) -> bool:
	if visited.has(controller_node.name):
		return false
	var next_visited := visited.duplicate()
	next_visited[controller_node.name] = true
	for input_node in trace_inputs(controller_node.name, connections):
		if not input_node.has_meta("brick_data"):
			continue
		var data: Dictionary = input_node.get_meta("brick_data")
		var brick_type := str(data.get("brick_type", ""))
		if brick_type == "sensor":
			return true
		if brick_type == "controller" and _controller_has_trigger_upstream(input_node, connections, next_visited):
			return true
	return false


func _controller_reaches_terminal(controller_node: GraphNode, connections: Array, visited: Dictionary) -> bool:
	if visited.has(controller_node.name):
		return false
	var data: Dictionary = controller_node.get_meta("brick_data")
	if str(data.get("brick_class", "")) == "ScriptController":
		return true
	var next_visited := visited.duplicate()
	next_visited[controller_node.name] = true
	for output_node in trace_outputs(controller_node.name, connections):
		if not output_node.has_meta("brick_data"):
			continue
		var output_data: Dictionary = output_node.get_meta("brick_data")
		var brick_type := str(output_data.get("brick_type", ""))
		if brick_type == "actuator":
			return true
		if brick_type == "controller" and _controller_reaches_terminal(output_node, connections, next_visited):
			return true
	return false


func _actuator_has_trigger_upstream(actuator_node: GraphNode, connections: Array) -> bool:
	for input_node in trace_inputs(actuator_node.name, connections):
		if not input_node.has_meta("brick_data"):
			continue
		var data: Dictionary = input_node.get_meta("brick_data")
		if str(data.get("brick_type", "")) == "controller" and _controller_has_trigger_upstream(input_node, connections, {}):
			return true
	return false


func extract_chains_from_graph(include_incomplete: bool = false) -> Array:
	var chains = []
	var connections = panel.graph_edit.get_connection_list()

	var controller_nodes = []
	for child in panel.graph_edit.get_children():
		if child is GraphNode and child.has_meta("brick_data"):
			var brick_data = child.get_meta("brick_data")
			if brick_data["brick_type"] == "controller":
				controller_nodes.append(child)

	for controller_node in controller_nodes:
		var chain = build_chain_from_controller(controller_node, connections)
		var is_script_controller = chain["controller"] != null and chain["controller"].get("type", "") == "ScriptController"
		var has_inputs = chain["sensors"].size() > 0 or chain.get("controller_inputs", []).size() > 0
		var has_actuators = chain["actuators"].size() > 0
		var is_complete = has_inputs and (has_actuators or is_script_controller)
		if is_complete or include_incomplete:
			var chain_name = get_chain_name_for_controller(controller_node)
			chains.append({
				"name": chain_name,
				"sensors": chain["sensors"],
				"controller_inputs": chain.get("controller_inputs", []),
				"controllers": [chain["controller"]] if chain["controller"] else [],
				"actuators": chain["actuators"]
			})

	return chains

func build_chain_from_controller(controller_node: GraphNode, connections: Array) -> Dictionary:
	var tree = build_controller_input_tree(controller_node, connections, {})
	var actuators = []

	var output_nodes = trace_outputs(controller_node.name, connections)
	for to_node in output_nodes:
		if to_node.has_meta("brick_data"):
			var brick_data = to_node.get_meta("brick_data")
			if brick_data["brick_type"] == "actuator":
				actuators.append(brick_data["brick_instance"].serialize())

	return {
		"sensors": tree["sensors"],
		"controller_inputs": tree["controller_inputs"],
		"controller": tree["controller"],
		"actuators": actuators
	}

func build_controller_input_tree(controller_node: GraphNode, connections: Array, visited: Dictionary) -> Dictionary:
	var sensors = []
	var controller_inputs = []
	var controller_brick = null

	if controller_node.has_meta("brick_data"):
		var brick_data = controller_node.get_meta("brick_data")
		controller_brick = brick_data["brick_instance"].serialize()

	if visited.has(controller_node.name):
		return {
			"sensors": [],
			"controller_inputs": [],
			"controller": controller_brick,
			"cycle": true
		}

	var next_visited = visited.duplicate()
	next_visited[controller_node.name] = true

	var input_nodes = trace_inputs(controller_node.name, connections)
	for from_node in input_nodes:
		if from_node.has_meta("brick_data"):
			var brick_data = from_node.get_meta("brick_data")
			if brick_data["brick_type"] == "sensor":
				sensors.append(brick_data["brick_instance"].serialize())
			elif brick_data["brick_type"] == "controller":
				controller_inputs.append(build_controller_input_tree(from_node, connections, next_visited))

	return {
		"sensors": sensors,
		"controller_inputs": controller_inputs,
		"controller": controller_brick
	}

func trace_inputs(node_name: String, connections: Array, visited: Dictionary = {}) -> Array:
	if visited.has(node_name):
		return []
	var next_visited = visited.duplicate()
	next_visited[node_name] = true
	var results = []
	for conn in connections:
		if conn["to_node"] == node_name:
			var from_node = panel.graph_edit.get_node_or_null(NodePath(conn["from_node"]))
			if from_node:
				if from_node.has_meta("is_reroute"):
					results.append_array(trace_inputs(from_node.name, connections, next_visited))
				else:
					results.append(from_node)
	return results

func trace_outputs(node_name: String, connections: Array, visited: Dictionary = {}) -> Array:
	if visited.has(node_name):
		return []
	var next_visited = visited.duplicate()
	next_visited[node_name] = true
	var results = []
	for conn in connections:
		if conn["from_node"] == node_name:
			var to_node = panel.graph_edit.get_node_or_null(NodePath(conn["to_node"]))
			if to_node:
				if to_node.has_meta("is_reroute"):
					results.append_array(trace_outputs(to_node.name, connections, next_visited))
				else:
					results.append(to_node)
	return results

func get_chain_name_for_controller(controller_node: GraphNode) -> String:
	if controller_node.has_meta("brick_data"):
		var brick_data = controller_node.get_meta("brick_data")
		var brick_class = brick_data["brick_class"]
		return brick_class.to_lower().replace("controller", "") + "_" + controller_node.name.replace("brick_node_", "")

	return controller_node.name.replace("brick_node_", "chain_")
